version "6.0"
#=======================================================
# ---- InnovMetric Software Inc.
# ---- Module  :    Workspace Manager
# ---- Version :    2023 IR11.2 (build 5602)
# ---- Date    :    Sunday, November 17, 2024 - 16:25:06
#-------------------------------------------------------
############################### POLYWORKS #################################
#
# ---- InnovMetric Software Inc.
# ---- Module  :    Workspace Manager
# ---- Version :    2023 IR11.3
# ---- Date    :    Nov 17, 24
#
############################### DISCLAIMER ################################
#
# This macro script is provided "AS IS", and may contain bugs, errors, and 
# other problems that could cause system or other failures and data loss. 
# InnovMetric Software Inc. disclaims any warranty or liability obligations 
# of any kind to the user of this macro.
#
############################### DESCRIPTION ###############################
#
#
#
################################ REVISION #################################
#
#
#
############################### PARAMETERS ################################
# [1][Input][string] project name (Possible Values: 620, 680)
DECLARE workspaceName_ $1

# [2][Input][string] inspector template name (Possible Values: A, B)
DECLARE inspectorTemplate_ $2

# [3][Input][String] Description (Possible Values:)
DECLARE partIdetifier_ $3

# [4][Output][Integer] Return Code (Possible Values: 200, 40x)
DECLARE returnCode_ $4

# [5][Output][String] Return Msg (Possible Values: "Successful")
DECLARE returnMsg_ $5

# FOR TEST
#SET workspaceName_ "680"
#SET inspectorTemplate_ "0452B37080"
#SET partIdetifier_ "376W99000047926045B37080"


################################ SUBMACROS ################################
############################# LOCAL VARIABLES #############################
DECLARE dlInspectorProjectId
DECLARE dlInspectorProjectName
DECLARE dlInspectionTemplateId
DECLARE dlInspectionTemplateName
DECLARE dlObjectType
DECLARE dlWorkspaceId
DECLARE inspectorID
############################# SHARED VARIABLES ############################
################################## CODE ###################################

# Check input argument
IF NOT HAS_VALUE ( workspaceName_ ) OR NOT HAS_VALUE ( inspectorTemplate_ )
    SET returnCode_ 404
    SET returnMsg_ "Can't find the PolyWorks inspection project and/or piece template"
    
    MACRO OUTPUT_ARGUMENT ( 4, $returnCode_ )
    MACRO OUTPUT_ARGUMENT ( 5, $returnMsg_ )
    MACRO END ( "Error" )
ENDIF

# Check Dataloop connection and try to login
DECLARE connectionName
DECLARE connectionStatus

DATALOOP CONNECTED GET ( connectionStatus )
IF $connectionStatus != "On"
    DATALOOP CONNECTION ACTIVE GET ( connectionName )
    IF NOT HAS_VALUE ( connectionName )
        SET returnCode_ 401
        SET returnMsg_ "Please finished the DataLoop connection parameter setup first."
        
        MACRO OUTPUT_ARGUMENT ( 4, $returnCode_ )
        MACRO OUTPUT_ARGUMENT ( 5, $returnMsg_ )
        MACRO END ( "Error" )
    ENDIF
    
    DATALOOP CONNECTION INTERFACE SHOW_PARAMETERS ( "Off" )
    DATALOOP CONNECTION INTERFACE ( "On" )
ENDIF


# Retrieve workspace id in dataloop by name
DATALOOP SEARCH ( "Workspace", $workspaceName_, dlWorkspaceId, "Exact Match")
IF NOT HAS_VALUE ( dlWorkspaceId )
    SET returnCode_ 403
    SET returnMsg_ "Cannot find this project in DataLoop: ${workspaceName_}."    
    MACRO OUTPUT_ARGUMENT ( 4, $returnCode_ )
    MACRO OUTPUT_ARGUMENT ( 5, $returnMsg_ )
    MACRO END ( "Error" )
ENDIF

# Retrieve template id in dataloop by name
DATALOOP SEARCH ( "Inspector piece template", $inspectorTemplate_, dlInspectionTemplateId, "Exact Match" )
IF NOT HAS_VALUE ( dlInspectionTemplateId )
    SET returnCode_ 403
    SET returnMsg_ "Cannot find this template in DataLoop: ${inspectorTemplate_}."
    MACRO OUTPUT_ARGUMENT ( 4, $returnCode_ )
    MACRO OUTPUT_ARGUMENT ( 5, $returnMsg_ )
    MACRO END ( "Error" )
ENDIF

# Find the template match the given workspace
DECLARE iter 1
DECLARE currentWorkspaceID
WHILE $iter <= SIZE ( dlInspectionTemplateId )
    # Retrieve project ID in Dataloop by template id
    DATALOOP OBJECT INFO GET ( $dlInspectionTemplateId[$iter], dlInspectionTemplateName, dlObjectType, dlInspectorProjectId )
    # Retrieve project name by project id in dataloop
    DATALOOP OBJECT INFO GET ( $dlInspectorProjectId, dlInspectorProjectName, dlObjectType, currentWorkspaceID )
    
    IF $dlWorkspaceId == $currentWorkspaceID
        BREAK
    ENDIF    
    ++iter
ENDWHILE

# Open project and create new piece with partIdentifier 
DATALOOP WORKSPACE OPEN ( $dlWorkspaceId )
MODULE POLYWORKS_INSPECTOR START USING_OBJECTS ( , $dlInspectorProjectName, inspectorID, "On", "Full" )
MACRO EXEC REMOTE_COMMAND ( "PIECE NEW", $inspectorID, $partIdetifier_, $inspectorTemplate_  )

SET returnCode_ 200
SET returnMsg_ "Start insection successful."
MACRO OUTPUT_ARGUMENT ( 4, $returnCode_ )
MACRO OUTPUT_ARGUMENT ( 5, $returnMsg_ )

